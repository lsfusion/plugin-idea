{
  psiImplUtilClass="com.lsfusion.migration.lang.psi.MigrationPsiImplUtil"

  parserClass="com.lsfusion.migration.lang.parser.MigrationParser"
  parserUtilClass="com.lsfusion.migration.lang.parser.MigrationParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Migration"
  psiImplClassSuffix="Impl"
  psiPackage="com.lsfusion.migration.lang.psi"
  psiImplPackage="com.lsfusion.migration.lang.psi.impl"
 
  elementTypeHolderClass="com.lsfusion.migration.lang.psi.MigrationTypes"
  elementTypeClass="com.lsfusion.migration.lang.psi.MigrationElementType"
  elementTypeFactory = "com.lsfusion.migration.lang.psi.MigrationElementTypeFactory.create"

  tokenTypeClass="com.lsfusion.migration.lang.psi.MigrationTokenType"

  extendedPin = false

  // additional tokens
  tokens = [
    COMMENTS='COMMENTS'
  ]
}



script ::= versionBlock*

versionBlock ::= versionLiteral versionBlockBody

versionLiteral ::= VERSION

versionBlockBody ::= LBRACE statement* RBRACE

statement ::=   propertyRename
            |   actionRename
            |   storedPropertyRename
            |   classRename
            |   tableRename
            |   navigatorElementRename
            |   objectRename
            |   propertyDrawRename
{
    recoverWhile = "statement_recover"
}

private statement_recover ::= !statement_start
private statement_start ::= STORED | FORM | PROPERTY | ACTION | CLASS | NAVIGATOR | TABLE | OBJECT | RBRACE
            
storedPropertyRename ::= STORED PROPERTY propertyOrActionFullRename {pin = 1}             
propertyRename ::= PROPERTY propertyOrActionFullRename {pin = 1}
actionRename ::= ACTION propertyOrActionFullRename {pin = 1}

propertyOrActionFullRename ::= compoundID signature ARROW compoundID (signature)?
 
classRename ::= CLASS sidRename {pin = 1}

tableRename ::= TABLE sidRename  {pin = 1}

navigatorElementRename ::= NAVIGATOR sidRename {pin = 1}

objectRename ::= OBJECT objectSidRename  {pin = 1}

objectSidRename ::= staticObjectID ARROW staticObjectID

propertyDrawRename ::= FORM PROPERTY formPropertyID ARROW formPropertyID {pin = 1}

formPropertyID ::= ID POINT ID POINT ID (LBRACKET idList RBRACKET)?   

staticObjectID ::= ID POINT ID POINT ID

sidRename ::= compoundID ARROW compoundID

signature ::= LSQBR (signatureItem (COMMA signatureItem)*)? RSQBR

signatureItem ::= UNKNOWNCLASS | resolveClassSet

resolveClassSet ::= concatenateClassSet
                  |	orObjectClassSet
                  |	upClassSet
                  |	singleClass
                    
concatenateClassSet ::= CONCAT RBRACKET resolveClassSetList LBRACKET

resolveClassSetList ::= (resolveClassSet (COMMA resolveClassSet)*)?

orObjectClassSet ::= LBRACE (upClassSet | customClass) (COMMA customClassList)? RBRACE

customClassList ::= (customClass (COMMA customClass)*)?
	
upClassSet ::=	LBRACKET customClassList RBRACKET
	
singleClass ::= customClass | PRIMITIVE_TYPE
	
customClass ::= compoundID

idList ::= (ID (COMMA ID)*)?

compoundID ::=  ID POINT ID
