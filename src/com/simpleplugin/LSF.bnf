
{
  psiImplUtilClass="com.simpleplugin.LSFPsiImplUtil"

  parserClass="com.simpleplugin.parser.LSFParser"
  parserUtilClass="com.simpleplugin.parser.GeneratedParserUtilBase"
  stubParserClass="com.simpleplugin.parser.GeneratedParserUtilBase"
 
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
 
  psiClassPrefix="LSF"
  psiImplClassSuffix="Impl"
  psiPackage="com.simpleplugin.psi"
  psiImplPackage="com.simpleplugin.psi.impl"
 
  elementTypeHolderClass="com.simpleplugin.psi.LSFTypes"
  elementTypeClass="com.simpleplugin.psi.LSFElementType"
  tokenTypeClass="com.simpleplugin.psi.LSFTokenType"
}

script ::= moduleHeader (scriptStatement*)
private scriptStatement ::= statement { recoverUntil = "script_statement_recover" }
private script_statement_recover ::= !def_statement_recover_start 

simpleName ::= ID
{mixin="com.simpleplugin.psi.LSFIdImpl" implements="com.simpleplugin.psi.LSFId"}

moduleName ::= simpleName;
requireList ::= REQUIRE nonEmptyModuleUsageList SEMI;
priorityList ::= PRIORITY nonEmptyNamespaceUsageList SEMI;
namespaceName ::= NAMESPACE namespaceUsage SEMI {
    mixin="com.simpleplugin.psi.declarations.impl.LSFExplicitNamespaceDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFExplicitNamespaceDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.ExplicitNamespaceStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
}
moduleHeader ::= MODULE moduleName SEMI
                 requireList?
                 priorityList?
                 namespaceName?
{mixin="com.simpleplugin.psi.declarations.impl.LSFModuleDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFModuleDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.ModuleStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    recoverUntil = "script_statement_recover"
}

// fake rules to generate types
fake_comments ::= COMMENTS
//fake_whitespaces ::= WHITESPACES

private statement ::=   classStatement
                    |	groupStatement
                    |	overrideStatement // starts with ID
                    |	constraintStatement
                    |	followsStatement // starts with ID
                    |	writeWhenStatement // starts with ID
                    |	propertyStatement // important to eat ID
                    |	eventStatement
                    |   showDepStatement
                    |	globalEventStatement
                    |	aspectStatement
                    |	tableStatement
                    |	loggableStatement
                    |	indexStatement
                    |	formStatement
                    |	designStatement
                    |	windowStatement
                    |	navigatorStatement
                    |	metaCodeStatement // ?
                    |	metaCodeDeclarationStatement // ?
                    |	emptyStatement
// Important ! after recover the rule should mandatory be pinned (matched)
private def_statement_recover_start ::= AFTER | BEFORE | (((CLASS (ABSTRACT | NATIVE)?) | ATSIGN | FORM | GROUP | META | DESIGN | TABLE | (WINDOW windowType))? ID) | CONSTRAINT | (EXTEND (CLASS | FORM | DESIGN) ID) | HIDE | INDEX | LOGGABLE | NAVIGATOR | ON | SEMI | SHOWDEP | WHEN

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// CLASS STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

classDecl ::= CLASS (ABSTRACT | NATIVE)? simpleNameWithCaption {
     mixin = "com.simpleplugin.psi.declarations.impl.LSFClassDeclarationImpl" implements = "com.simpleplugin.psi.declarations.LSFClassDeclaration"
     stubClass = "com.simpleplugin.psi.stubs.ClassStubElement"
     elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
}

extendingClassDeclaration ::= EXTEND CLASS customClassUsage

classStatement ::= (classDecl | extendingClassDeclaration) classInstancesAndParents {pin = 1
    mixin="com.simpleplugin.psi.extend.impl.LSFClassExtendImpl" implements="com.simpleplugin.psi.extend.LSFClassExtend"
    stubClass = "com.simpleplugin.psi.stubs.extend.ExtendClassStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
}

private classInstancesAndParents ::=    LBRACE staticObjectDeclList RBRACE (classParentsList SEMI)?
		                            |   (classParentsList)? SEMI

classParentsList ::= COLON nonEmptyCustomClassUsageList

staticObjectDeclList ::= (nonEmptyStaticObjectDeclList)?

nonEmptyStaticObjectDeclList ::= staticObjectDecl (COMMA staticObjectDecl)*

staticObjectDecl ::= simpleNameWithCaption
{mixin="com.simpleplugin.psi.declarations.impl.LSFStaticObjectDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFStaticObjectDeclaration"}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// GROUP STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

groupStatement ::= GROUP simpleNameWithCaption (COLON groupUsage)? SEMI
{mixin="com.simpleplugin.psi.declarations.impl.LSFGroupDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFGroupDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.GroupStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 2
}


////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// PROPERTY STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

propertyStatement ::= propertyDeclaration EQUAL
                      (expressionUnfriendlyPD | propertyExpression)
                      propertyOptions
                      (SEMI)?
{mixin="com.simpleplugin.psi.declarations.impl.LSFGlobalPropDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFGlobalPropDeclaration, com.simpleplugin.psi.context.ModifyParamContext"
    stubClass = "com.simpleplugin.psi.stubs.PropStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 1
    methods = [ getContextModifier getContextInferrer ]
}

propertyDeclaration ::= simpleNameWithCaption (LBRAC classParamDeclareList RBRAC)?
{methods = [ resolveParamDecls ]}

paramDeclare ::= simpleName
{mixin="com.simpleplugin.psi.declarations.impl.LSFParamDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFParamDeclaration"}

classParamDeclare ::= (className paramDeclare) | paramDeclare
{implements="com.simpleplugin.psi.context.ClassParamDeclareContext"
 methods = [ resolveClass ensureClass ]}

classParamDeclareList ::= (nonEmptyClassParamDeclareList)?
nonEmptyClassParamDeclareList ::= classParamDeclare (COMMA classParamDeclare)*

propertyExpression ::= ifPE
{methods = [ resolveValueClass resolveInferredValueClass resolveParams inferParamClasses]}

ifPE ::= orPE (IF orPE)*
orPE ::= xorPE (OR xorPE)*
xorPE ::= andPE (XOR andPE)*
andPE ::= notPE (AND notPE)*
notPE ::= (NOT notPE) | equalityPE

equalityPE ::= relationalPE (EQ_OPERAND relationalPE)?
relationalPE ::= additiveORPE (((LESS | GREATER | LESS_EQUALS | GREATER_EQUALS) additiveORPE) | typePropertyDefinition)?
additiveORPE ::= additivePE (ADDOR_OPERAND additivePE)*
additivePE ::= multiplicativePE ((PLUS | MINUS) multiplicativePE)*
multiplicativePE ::= unaryMinusPE (MULT_OPERAND unaryMinusPE)*
unaryMinusPE ::= (MINUS unaryMinusPE) | postfixUnaryPE
postfixUnaryPE ::= simplePE (LSQBR uintLiteral RSQBR)?
simplePE ::= (LBRAC propertyExpression RBRAC) | expressionPrimitive
expressionPrimitive ::= <<fullCompoundParamDeclareCheck>> expressionFriendlyPD | exprParameterUsage

expressionFriendlyPD ::= joinPropertyDefinition
	    |	multiPropertyDefinition
	    |	overridePropertyDefinition
	    |	ifElsePropertyDefinition
	    |	maxPropertyDefinition
	    |	casePropertyDefinition
	    |	partitionPropertyDefinition
	    |	recursivePropertyDefinition
	    |	structCreationPropertyDefinition
	    |	concatPropertyDefinition
	    |	castPropertyDefinition
	    |	sessionPropertyDefinition
	    |	signaturePropertyDefinition
	    |	literal

expressionUnfriendlyPD ::= contextIndependentPD | actionPropertyDefinition
{methods = [ resolveUnfriendValueClass resolveValueParamClasses]}

contextIndependentPD ::= dataPropertyDefinition
                    |   nativePropertyDefinition
	                |   abstractActionPropertyDefinition
                    |   abstractPropertyDefinition
	                |   formulaPropertyDefinition
	                |   groupPropertyDefinition
                    |   filterPropertyDefinition

joinPropertyDefinition ::= JOIN? propertyObject LBRAC propertyExpressionList RBRAC
{implements = "com.simpleplugin.psi.context.FriendlyPE, com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveInferredValueClass resolveParamClasses inferParamClasses getParamList ]}

overridePropertyDefinition ::= (OVERRIDE | EXCLUSIVE) nonEmptyPropertyExpressionList
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

ifElsePropertyDefinition ::= IF propertyExpression
                                    THEN propertyExpression
                                    (ELSE propertyExpression)?   // check greedy
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

maxPropertyDefinition ::= (MAX | MIN) nonEmptyPropertyExpressionList
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses ]}

caseBranchBody ::= WHEN propertyExpression THEN propertyExpression
casePropertyDefinition ::= CASE (exclusiveOverrideOption)?
                           (caseBranchBody)+
                           (ELSE propertyExpression)?
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses ]}

multiPropertyDefinition ::= MULTI nonEmptyPropertyExpressionList (exclusiveOverrideOption)?
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses ]}

partitionPropertyBy ::= BY nonEmptyPropertyExpressionList 
partitionPropertyDefinition ::= PARTITION
                                (   (SUM | PREV)
                                |   UNGROUP propertyObject
                                    (   PROPORTION (STRICT)? ROUND LBRAC LEX_UINT_LITERAL RBRAC
                                    |   LIMIT (STRICT)?
                                    )
                                )
                                propertyExpression (partitionPropertyBy)?
                                (ORDER (DESC )? nonEmptyPropertyExpressionList)?
                                (WINDOW EXCEPTLAST)?
{implements = "com.simpleplugin.psi.context.FriendlyPE, com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveInferredValueClass resolveParamClasses inferParamClasses getParamList ]}

recursivePropertyDefinition ::= RECURSION propertyExpression STEP propertyExpression (CYCLES (YES |	NO | IMPOSSIBLE))?
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

structCreationPropertyDefinition ::= STRUCT LBRAC nonEmptyPropertyExpressionList RBRAC
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

castPropertyDefinition ::= builtInClassName LBRAC propertyExpression RBRAC
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

concatPropertyDefinition ::= CONCAT stringLiteral COMMA nonEmptyPropertyExpressionList
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

sessionPropertyType ::= PREV | CHANGED | SET | DROPPED | SETCHANGED | DROPCHANGED | DROPSET 
sessionPropertyDefinition ::= sessionPropertyType LBRAC propertyExpression RBRAC
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

signaturePropertyDefinition ::= (CLASS) LBRAC propertyExpression RBRAC
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses]}

propertyExprObject ::= (LSQBR (propertyExpression | expressionUnfriendlyPD) RSQBR)
{implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer ] }

propertyObject ::=  propertyUsage | propertyExprObject
noContextPropertyUsage ::= propertyUsage
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}


dataPropertyDefinition ::= DATA (SESSION)? className LBRAC classNameList RBRAC
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

nativePropertyDefinition ::= NATIVE className LBRAC classNameList RBRAC
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

abstractPropertyDefinition ::= ABSTRACT (((CASE | MULTI) (OVERRIDE | EXCLUSIVE)?) | OVERRIDE | EXCLUSIVE)? (CHECKED)? className LBRAC classNameList RBRAC
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

formulaPropertyDefinition ::= FORMULA (builtInClassName)? stringLiteral
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

groupingType ::= SUM | MAX | MIN | CONCAT | AGGR | GEQUAL | LAST
groupPropertyBy ::= BY nonEmptyPropertyExpressionList;
orderPropertyBy ::= ORDER (DESC)? nonEmptyPropertyExpressionList 
groupPropertyDefinition ::= GROUP groupingType
		                    nonEmptyPropertyExpressionList
		                    (groupPropertyBy)?
		                    (orderPropertyBy)?
		                    (WHERE propertyExpression)?
{implements="com.simpleplugin.psi.context.ModifyParamContext, com.simpleplugin.psi.context.UnfriendlyPE" methods = [ getContextModifier getContextInferrer resolveUnfriendValueClass resolveValueParamClasses ] }		                    

filterPropertyDefinition ::= (FILTER | ORDER | VIEW) groupObjectID
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

typeIs ::= IS | AS
typePropertyDefinition ::= typeIs className


propertyOptions ::= (   IN groupUsage
                    |	PERSISTENT
                    |   COMPLEX
                    |	TABLE tableUsage
                    |	shortcutSetting
                    |	asEditActionSetting
                    |	toolbarSetting
                    |	fixedCharWidthSetting
                    |	minCharWidthSetting
                    |	maxCharWidthSetting
                    |	prefCharWidthSetting
                    |	imageSetting
                    |	editKeySetting
                    |	autosetSetting
                    |	confirmSetting
                    |	regexpSetting
                    |	loggableSetting
                    |	echoSymbolsSetting
                    |	indexSetting
                    |	aggPropSetting
                    |	notNullSetting
                    |	onEditEventSetting
                    |	eventIdSetting
                    )*

shortcutSetting ::= SHORTCUT noContextPropertyUsage
asEditActionSetting ::= (ASONCHANGE | ASONCHANGEWYS | ASONEDIT) noContextPropertyUsage
toolbarSetting ::= TOOLBAR
fixedCharWidthSetting ::= FIXEDCHARWIDTH intLiteral
minCharWidthSetting ::= MINCHARWIDTH intLiteral
maxCharWidthSetting ::= MAXCHARWIDTH intLiteral
prefCharWidthSetting ::= PREFCHARWIDTH intLiteral
imageSetting ::= IMAGE stringLiteral
editKeySetting ::= EDITKEY stringLiteral (SHOW | HIDE)?
autosetSetting ::= AUTOSET
confirmSetting ::= CONFIRM
regexpSetting ::= REGEXP stringLiteral (stringLiteral)?
loggableSetting ::= LOGGABLE
echoSymbolsSetting ::= ECHO
indexSetting ::= INDEXED
aggPropSetting ::= AGGPROP
notNullSetting ::= NOT nullOption (DELETE)? baseEvent
onEditEventSetting ::= ON (CHANGE | CHANGEWYS | EDIT) actionPropertyDefinitionBody
eventIdSetting ::= EVENTID stringLiteral

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// ACTION PROPERTIES ///////////////////////////
////////////////////////////////////////////////////////////////////////////////

actionPropertyDefinition ::= ACTION
		                    (LBRAC exprParameterUsageList RBRAC)?
		                    actionPropertyDefinitionBody
{implements="com.simpleplugin.psi.context.ExtendParamContext" methods = [ getContextModifier getContextInferrer ] } // actually declare context, but it's pretty difficult to implement using only classes


abstractActionPropertyDefinition ::= ABSTRACT ACTION
                                    (   CASE (exclusiveOverrideOption)?
                                    |	MULTI (exclusiveOverrideOption )?
                                    |	LIST
                                    )?
                                    (CHECKED)?
                                    LBRAC classNameList RBRAC
{implements = "com.simpleplugin.psi.context.UnfriendlyPE" methods = [ resolveUnfriendValueClass resolveValueParamClasses ]}

// extendContextActionPDB comes before keepContextActionPDB to check assign action before exec action
actionPropertyDefinitionBody ::=    customActionPDB
                                |   extendContextActionPDB
                                |	keepContextActionPDB
{implements = "com.simpleplugin.psi.context.ActionExpression" methods = [ inferActionParamClasses ]}

private extendContextActionPDB ::=  assignActionPropertyDefinitionBody
                        |	forActionPropertyDefinitionBody
                        |   whileActionPropertyDefinitionBody
                        |	changeClassActionPropertyDefinitionBody
                        |	deleteActionPropertyDefinitionBody
                        |	addObjectActionPropertyDefinitionBody

private keepContextActionPDB ::=    listActionPropertyDefinitionBody
                        |	requestInputActionPropertyDefinitionBody
                        |	execActionPropertyDefinitionBody
                        |	ifActionPropertyDefinitionBody
                        |	caseActionPropertyDefinitionBody
                        |	multiActionPropertyDefinitionBody
                        |	terminalFlowActionPropertyDefinitionBody

private customActionPDB ::= formActionPropertyDefinitionBody
                |	addFormActionPropertyDefinitionBody
                |	editFormActionPropertyDefinitionBody
                |	customActionPropertyDefinitionBody
                |   messageActionPropertyDefinitionBody
                |   asyncUpdateActionPropertyDefinitionBody
                |   seekObjectActionPropertyDefinitionBody
                |   confirmActionPropertyDefinitionBody
                |   emailActionPropertyDefinitionBody
                |	fileActionPropertyDefinitionBody
                |	evalActionPropertyDefinitionBody

formActionObjectUsage ::= objectUsage EQUAL propertyExpression 
formActionPropertyDefinitionBody ::=
	    FORM formUsage
		(OBJECTS formActionObjectList)?
        (CONTEXTFILTER formActionObjectUsage)?
		(formSessionScopeLiteral)?
		(modalityTypeLiteral)?
		(CHECK)? (SHOWDROP)?
		(formPrintTypeLiteral)?
{implements="com.simpleplugin.psi.context.ActionExpression, com.simpleplugin.psi.context.FormContext" methods = [inferActionParamClasses] }

formActionObjectList ::=
		formActionObjectUsage
		(COMMA formActionObjectUsage)*

customActionPropertyDefinitionBody ::= CUSTOM stringLiteral (LBRAC classNameList RBRAC)?
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

addFormActionPropertyDefinitionBody ::= ADDFORM (SESSION)? customClassUsage
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }
editFormActionPropertyDefinitionBody ::= EDITFORM (SESSION)? customClassUsage
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

addObjectActionPropertyDefinitionBody ::= ADDOBJ customClassUsage
		                                    (WHERE propertyExpression)?
		                                    (TO mappedPropertyExprParam)?
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }

emailActionPropertyDefinitionBody ::=
		EMAIL
		(FROM propertyExpression)?
		SUBJECT propertyExpression
		(emailRecipientTypeLiteral propertyExpression)*
		(	INLINE formEmailToObjects
		|	ATTACH emailAttachFormat (NAME propertyExpression)? formEmailToObjects
		)*
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

formEmailToObjects ::= formUsage emailActionFormObjects
{implements="com.simpleplugin.psi.context.FormContext"}

emailActionFormObjects ::=
        (   OBJECTS
			formActionObjectList
		)?
	;

confirmActionPropertyDefinitionBody ::= CONFIRM propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }
messageActionPropertyDefinitionBody ::= MESSAGE propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

asyncUpdateActionPropertyDefinitionBody ::= ASYNCUPDATE propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

seekObjectActionPropertyDefinitionBody ::= SEEK objectID propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

fileActionPropertyDefinitionBody ::= (LOADFILE | OPENFILE) propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

changeClassActionPropertyDefinitionBody ::= CHANGECLASS exprParameterUsage TO customClassUsage (WHERE propertyExpression)?
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }

deleteActionPropertyDefinitionBody ::= DELETE exprParameterUsage (WHERE propertyExpression)?
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }

evalActionPropertyDefinitionBody ::= EVAL propertyExpression
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

requestInputActionPropertyDefinitionBody ::= REQUEST typeId (INPUT | (ID)? actionPropertyDefinitionBody)
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

listActionPropertyDefinitionBody ::=    (NEWSESSION (AUTOAPPLY)? (SINGLE)?)?
		                                LBRACE
			                            (	actionPropertyDefinitionBody (SEMI)? // ;?
			                            |	localDataPropertyDefinition SEMI
			                            |	emptyStatement
			                            )*
		                                RBRACE
{mixin="com.simpleplugin.psi.LSFListActionImpl" implements="com.simpleplugin.psi.LSFListAction, com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

localDataPropertyDefinition ::= LOCAL simpleName EQUAL className LBRAC classNameList RBRAC
{mixin="com.simpleplugin.psi.declarations.impl.LSFLocalPropDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFLocalPropDeclaration"}

execActionPropertyDefinitionBody ::= (EXEC)? propertyObject LBRAC propertyExpressionList RBRAC
{implements = "com.simpleplugin.psi.context.PropertyUsageContext, com.simpleplugin.psi.context.ActionExpression" methods = [ resolveParamClasses inferActionParamClasses getParamList ]}

assignActionPropertyDefinitionBody ::= (ASSIGN)?
                                       mappedPropertyExprParam
                                       ARROW
                                       propertyExpression (WHERE propertyExpression)?
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }

ifActionPropertyDefinitionBody ::= IF propertyExpression
		                                THEN actionPropertyDefinitionBody
		                                (ELSE actionPropertyDefinitionBody)?
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

caseActionPropertyDefinitionBody ::= CASE (exclusiveOverrideOption)?
			                            (actionCaseBranchBody)+
			                            (ELSE actionPropertyDefinitionBody)?
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

actionCaseBranchBody ::= WHEN propertyExpression THEN actionPropertyDefinitionBody

multiActionPropertyDefinitionBody ::= MULTI (exclusiveOverrideOption)? nonEmptyActionPDBList
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

forAddObjClause ::= ADDOBJ (paramDeclare EQUAL)? customClassUsage
{implements="com.simpleplugin.psi.context.ClassParamDeclareContext"
 methods = [ resolveClass ensureClass ]}

forActionPropertyDefinitionBody ::= FOR (propertyExpression (ORDER (DESC)? nonEmptyPropertyExpressionList)? )?
		                            inlineStatement
		                            (forAddObjClause)?
		                            DO actionPropertyDefinitionBody
		                            (ELSE actionPropertyDefinitionBody)?
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }
                                                       
whileActionPropertyDefinitionBody ::= WHILE (propertyExpression (ORDER (DESC)? nonEmptyPropertyExpressionList)? )?
		                              inlineStatement
		                              (forAddObjClause)?
		                              DO actionPropertyDefinitionBody
{implements="com.simpleplugin.psi.context.ExtendParamContext, com.simpleplugin.psi.context.ActionExpression" methods = [ getContextModifier getContextInferrer inferActionParamClasses ] }

terminalFlowActionPropertyDefinitionBody ::= BREAK | RETURN
{implements="com.simpleplugin.psi.context.ActionExpression" methods = [inferActionParamClasses] }

////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////// FORM STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

formStatement ::=	(	formDecl
            		|	extendingFormDeclaration
		            )
                    (	formGroupObjectsList
                    |	formTreeGroupObjectList
                    |	formFiltersList
                    |	formPropertiesList
                    |	formHintsList
                    |	formEventsList
                    |	filterGroupDeclaration
                    |	formOrderByList
                    |	dialogFormDeclaration
                    |	editFormDeclaration
                    |	listFormDeclaration
                    )*
                    SEMI {pin=1
    mixin="com.simpleplugin.psi.extend.impl.LSFFormExtendImpl" implements="com.simpleplugin.psi.extend.LSFFormExtend"
    stubClass = "com.simpleplugin.psi.stubs.extend.ExtendFormStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
}

dialogFormDeclaration ::= DIALOG customClassUsage OBJECT ID
editFormDeclaration ::= EDIT customClassUsage OBJECT ID
listFormDeclaration ::= LIST customClassUsage OBJECT ID

formDecl ::= FORM simpleNameWithCaption
		            (TITLE stringLiteral)?
		            (modalityTypeLiteral)?
		            (IMAGE stringLiteral)?
		            (AUTOREFRESH intLiteral)?
{mixin="com.simpleplugin.psi.declarations.impl.LSFFormDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFFormDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.FormStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 2
}

extendingFormDeclaration ::= EXTEND FORM formUsage

formGroupObjectsList ::= OBJECTS formGroupObjectDeclaration
		                         (COMMA formGroupObjectDeclaration)*

formTreeGroupObjectList ::= TREE (ID)? formTreeGroupObjectDeclaration
		                               (COMMA formTreeGroupObjectDeclaration)*

formGroupObjectDeclaration ::= formCommonGroupObject (formGroupObjectReportPath)? (formGroupObjectViewType)? (formGroupObjectPageSize)?
formTreeGroupObjectDeclaration ::= formCommonGroupObject (treeGroupParentDeclaration)?
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}

treeGroupParentDeclaration ::= PARENT (propertyUsage | LBRAC nonEmptyPropertyUsageList RBRAC)

formCommonGroupObject ::= formSingleGroupObjectDeclaration | formMultiGroupObjectDeclaration
{mixin="com.simpleplugin.psi.declarations.impl.LSFGroupObjectDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFGroupObjectDeclaration"}

formGroupObjectReportPath ::= REPORTFILE mappedPropertyObject

formGroupObjectViewType ::= (INIT | FIXED ) classViewType

classViewType ::= PANEL | HIDE | GRID

formGroupObjectPageSize ::= PAGESIZE intLiteral

formSingleGroupObjectDeclaration ::= formObjectDeclaration

formMultiGroupObjectDeclaration ::= (simpleName EQUAL)?
		                            LBRAC
			                        formObjectDeclaration (COMMA formObjectDeclaration)+
		                            RBRAC

formObjectDeclaration ::= (simpleName EQUAL)?
		                  className (stringLiteral)?
		                  (ON CHANGE formActionPropertyObject)?
{mixin="com.simpleplugin.psi.declarations.impl.LSFObjectDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFObjectDeclaration"}

formPropertyDrawNameDecl ::= (simpleName EQUAL)? formPropertyName formPropertyOptionsList
{mixin="com.simpleplugin.psi.declarations.impl.LSFPropertyDrawNameDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFPropertyDrawNameDeclaration"}
formPropertiesNamesDeclList ::=	formPropertyDrawNameDecl (COMMA formPropertyDrawNameDecl)*
formMappedNamePropertiesList ::= LBRAC objectUsageList RBRAC formPropertyOptionsList formPropertiesNamesDeclList 
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}

formPropertiesList ::=  PROPERTIES formMappedNamePropertiesList 
	                |	PROPERTIES formPropertyOptionsList formMappedPropertiesList

formPropertyOptionsList ::= (	propertyEditTypeLiteral
                            |	HINTNOUPDATE
                            |   HINTTABLE
                            |   TOOLBAR
                            |	COLUMNS LBRAC nonEmptyGroupObjectUsageList RBRAC
                            |	SHOWIF formCalcPropertyObject
                            |	READONLYIF formCalcPropertyObject
                            |	BACKGROUND formCalcPropertyObject
                            |	FOREGROUND formCalcPropertyObject
                            |	HEADER formCalcPropertyObject
                            |	FOOTER formCalcPropertyObject
                            |	FORCE classViewType
                            |	TODRAW formGroupObjectEntity
                            |	BEFORE formPropertyDraw
                            |	AFTER  formPropertyDraw
                            |   QUICKFILTER formPropertyDraw
                            |	ON EDIT formActionPropertyObject
                            |	ON CHANGE formActionPropertyObject
                            |	ON CHANGEWYS formActionPropertyObject
                            |	ON SHORTCUT (stringLiteral)? formActionPropertyObject
                            |	EVENTID stringLiteral
                            )*

formPropertyDraw ::= mappedPropertyObject | ID

formPropertyDrawMappedDecl ::= (simpleName EQUAL)? formMappedProperty formPropertyOptionsList
{mixin="com.simpleplugin.psi.declarations.impl.LSFPropertyDrawMappedDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFPropertyDrawMappedDeclaration"}

formMappedPropertiesList ::= formPropertyDrawMappedDecl
		                     (COMMA formPropertyDrawMappedDecl)*

formCalcPropertyObject ::= mappedPropertyObject
formActionPropertyObject ::= mappedPropertyObject

formGroupObjectEntity ::= groupObjectUsage
formMappedProperty ::= formPropertyName LBRAC objectUsageList RBRAC
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}

predefinedFormPropertyName ::= OBJVALUE
                           |	SELECTION
                           |	ADDOBJ
                           |	ADDFORM
                           |	ADDSESSIONFORM
                           |	EDITFORM
                           |	EDITSESSIONFORM
                           |	DELETE
                           |	DELETESESSION
{mixin="com.simpleplugin.psi.LSFIdImpl" implements="com.simpleplugin.psi.LSFId"}
 
formPropertyName ::= propertyUsage |	predefinedFormPropertyName

formFiltersList ::= FILTERS formFilterDeclaration (COMMA formFilterDeclaration)*

formHintsList ::= (HINTNOUPDATE | HINTTABLE) LIST nonEmptyNoContextPropertyUsageList

formEventsList ::= EVENTS formEventDeclaration (COMMA formEventDeclaration)*

formEventDeclaration ::= ON
                        (	OK
                        |	APPLY
                        |	CLOSE
                        |	INIT
                        |	CANCEL
                        |	DROP
                        |	QUERYOK
                        |	QUERYCLOSE
                        | 	CHANGE ID
                        )
                        formActionPropertyObject

filterGroupDeclaration ::= FILTERGROUP ID
		                   (FILTER stringLiteral stringLiteral formFilterDeclaration filterSetDefault)+

formFilterDeclaration ::= propertyExpression
filterSetDefault ::= (DEFAULT)?

formOrderByList ::= ORDER BY formPropertyDrawWithOrder
		                     (COMMA formPropertyDrawWithOrder)*

formPropertyDrawWithOrder ::= formPropertyDraw (ASC | DESC)?


////////////////////////////////////////////////////////////////////////////////
/////////////////////////////OVERRIDE STATEMENT/////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

overrideStatement ::= mappedPropertyClassParamDeclare PLUSEQ
		                (WHEN propertyExpression THEN)?
		                (	propertyExpression
		                |	actionPropertyDefinition
		                )
		                (SEMI)?
{implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ]}		                    

		                // ;?

////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// CONSTRAINT STATEMENT //////////////////////////
////////////////////////////////////////////////////////////////////////////////

constraintStatement ::= CONSTRAINT baseEvent propertyExpression
		                (CHECKED (BY nonEmptyNoContextPropertyUsageList)? )?
		                MESSAGE stringLiteral
		                SEMI {pin = 1 implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] }

////////////////////////////////////////////////////////////////////////////////
///////////////////////////////// FOLLOWS STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

followsStatement ::= mappedPropertyClassParamDeclare FOLLOWS
                     propertyExpression (RESOLVE followsResolveType baseEvent)?
		             (COMMA propertyExpression (RESOLVE followsResolveType baseEvent)?)*
		             SEMI
		             {pin = 2 implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] }

followsResolveType ::= LEX_LOGICAL_LITERAL | ALL | NOTHING

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// WRITE STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

writeWhenStatement ::= mappedPropertyClassParamDeclare ARROW propertyExpression WHEN (DO)? propertyExpression SEMI
{pin = 2 implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] }

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// EVENT STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

eventStatement ::= WHEN baseEvent propertyExpression inlineStatement
		                DO actionPropertyDefinitionBody (ORDER (DESC)? nonEmptyPropertyExpressionList)?
		           (SEMI)? {pin = 1 implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] } // Разобраться с точкой с запятой

////////////////////////////////////////////////////////////////////////////////
//////////////////////////// GLOBAL EVENT STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

globalEventStatement ::= ON baseEvent (SINGLE)? (SHOWDEP ID)? actionPropertyDefinitionBody (SEMI)? {pin=1
implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] } // ;?

baseEvent ::= (APPLY | SESSION)? (FORMS nonEmptyFormUsageList)? (GOAFTER nonEmptyNoContextPropertyUsageList)?

inlineStatement ::=   (NOINLINE (LBRAC exprParameterUsageList RBRAC)? )? (INLINE)?

////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// SHOWDEP STATEMENT //////////////////////////////
////////////////////////////////////////////////////////////////////////////////

showDepStatement ::= SHOWDEP noContextPropertyUsage FROM noContextPropertyUsage SEMI {pin=1 methods = [ getIcon ]}

////////////////////////////////////////////////////////////////////////////////
//////////////////////////////// ASPECT STATEMENT //////////////////////////////
////////////////////////////////////////////////////////////////////////////////

aspectStatement ::= (BEFORE | AFTER) mappedPropertyClassParamDeclare DO actionPropertyDefinitionBody (SEMI)? {pin=1 
 implements="com.simpleplugin.psi.context.ModifyParamContext" methods = [ getContextModifier getContextInferrer getIcon ] } // ;?


////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// TABLE STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

tableStatement ::= TABLE simpleName LBRAC nonEmptyClassNameList RBRAC SEMI 
{mixin="com.simpleplugin.psi.declarations.impl.LSFTableDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFTableDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.TableStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 2
}


////////////////////////////////////////////////////////////////////////////////
/////////////////////////////// LOGGABLE STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

loggableStatement ::= LOGGABLE nonEmptyNoContextPropertyUsageList SEMI {pin=1 methods = [ getIcon ]}

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// INDEX STATEMENT /////////////////////////////
////////////////////////////////////////////////////////////////////////////////

indexStatement ::= INDEX nonEmptyNoContextPropertyUsageList SEMI {pin=1 methods = [ getIcon ]}

////////////////////////////////////////////////////////////////////////////////
/////////////////////////////// WINDOW STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

windowStatement ::= windowCreateStatement | windowHideStatement {pin=1 methods = [ getIcon ]}

windowCreateStatement ::= WINDOW windowType simpleName stringLiteral windowOptions SEMI
{mixin="com.simpleplugin.psi.declarations.impl.LSFWindowDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFWindowDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.WindowStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 3
}

windowHideStatement ::= HIDE WINDOW windowUsage SEMI {pin=2}

windowType ::= MENU | PANEL | TOOLBAR | TREE

windowOptions ::=   (	HIDETITLE
                    |	DRAWROOT
                    |	HIDESCROLLBARS
                    |	orientation
                    |	dockPosition
                    |	borderPosition
                    |	HALIGN LBRAC alignmentLiteral RBRAC
                    |	VALIGN LBRAC alignmentLiteral RBRAC
                    |	TEXTHALIGN LBRAC alignmentLiteral RBRAC
                    |	TEXTVALIGN LBRAC alignmentLiteral RBRAC
                    )*

borderPosition ::= LEFT	| RIGHT | TOP | BOTTOM
orientation ::= VERTICAL | HORIZONTAL
dockPosition ::= POSITION LBRAC intLiteral COMMA intLiteral COMMA intLiteral COMMA intLiteral RBRAC

////////////////////////////////////////////////////////////////////////////////
/////////////////////////////// NAVIGATOR STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

navigatorStatement ::= NAVIGATOR navigatorElementStatementBody {pin=1 methods = [ getIcon ]}

navigatorElementStatementBody ::=   (   LBRACE
                                        (	addNavigatorElementStatement
                                        |	newNavigatorElementStatement
                                        |	setupNavigatorElementStatement
                                        |	emptyStatement
                                        )*
                                        RBRACE
                                    ) | emptyStatement

addNavigatorElementStatement ::= ADD navigatorElementSelector (stringLiteral)? navigatorElementInsertPositionSelector (TO windowUsage)? navigatorElementStatementBody

newNavigatorElementStatement ::= NEW simpleName (ACTION noContextPropertyUsage)? stringLiteral navigatorElementInsertPositionSelector (TO windowUsage)? (IMAGE stringLiteral)? navigatorElementStatementBody
{mixin="com.simpleplugin.psi.declarations.impl.LSFNavigatorElementDeclarationImpl" implements="com.simpleplugin.psi.declarations.LSFNavigatorElementDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.NavigatorElementStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
    pin = 2
}

navigatorElementInsertPositionSelector ::=	(IN navigatorElementSelector)?
                                            (   insertRelativePositionLiteral navigatorElementSelector
                                            |	FIRST
                                            )?

setupNavigatorElementStatement ::= navigatorElementSelector (stringLiteral)? (TO windowUsage)? navigatorElementStatementBody

navigatorElementSelector ::= navigatorElementUsage

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// DESIGN STATEMENT ////////////////////////////
////////////////////////////////////////////////////////////////////////////////

designStatement ::=	(designDeclaration | extendDesignDeclaration) componentStatementBody {pin=1 methods = [ getIcon ]}

designDeclaration ::= DESIGN formUsage (stringLiteral)? (FROM DEFAULT)? {pin=2}

extendDesignDeclaration ::= EXTEND DESIGN formUsage

componentStatementBody ::=  LBRACE
                            (	setObjectPropertyStatement
                            |	positionComponentsStatement
                            |	setupComponentStatement
                            |	setupGroupObjectStatement
                            |	newComponentStatement
                            |	addComponentStatement
                            |	removeComponentStatement
                            |	emptyStatement
                            )*
		                    RBRACE
	                    |	emptyStatement

setupComponentStatement ::= componentSelector componentStatementBody

setupGroupObjectStatement ::= GROUP LBRAC ID RBRAC
                              LBRACE
                              (   setObjectPropertyStatement
                              |   emptyStatement
                              )*
                              RBRACE

newComponentStatement ::= NEW multiCompoundID componentInsertPositionSelector componentStatementBody
addComponentStatement ::= ADD componentSelector componentInsertPositionSelector componentStatementBody

componentInsertPositionSelector ::= (IN componentSelector)?
                                    (   insertRelativePositionLiteral componentSelector
                                    |	FIRST
                                    )?

removeComponentStatement ::= REMOVE componentSelector (CASCADE)? SEMI

componentSelector ::=   PARENT LBRAC componentSelector RBRAC
                	|	PROPERTY LBRAC propertySelector RBRAC
                    |	multiCompoundID

propertySelector ::= formMappedProperty | compoundID // compoundID?
{mixin="com.simpleplugin.psi.references.impl.LSFPropertyDrawReferenceImpl" implements="com.simpleplugin.psi.references.LSFPropertyDrawReference"}

positionComponentsStatement ::= POSITION componentSelector simplexConstraintLiteral (componentSelector)? SEMI

setObjectPropertyStatement ::= ID EQUAL componentPropertyValue SEMI

componentPropertyValue ::=  colorLiteral
                        |	stringLiteral
                        |	intLiteral
                        |	doubleLiteral
                        |	dimensionLiteral
                        |	booleanLiteral
                        |	simplexConstraintLiteral
                        |	boundsIntLiteral
                        |	boundsDoubleLiteral
                        |   containerTypeLiteral
                        |   flexAlignmentLiteral

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////// META STATEMENT //////////////////////////////
////////////////////////////////////////////////////////////////////////////////

// recoverUntil=rule_recover
metaCodeDeclarationStatement ::= META simpleName LBRAC metaDeclIdList RBRAC anyTokens END {pin=2
    mixin = "com.simpleplugin.psi.declarations.impl.LSFMetaDeclarationImpl" implements = "com.simpleplugin.psi.declarations.LSFMetaDeclaration"
    stubClass = "com.simpleplugin.psi.stubs.MetaStubElement"
    elementTypeFactory = "com.simpleplugin.psi.stubs.types.LSFStubFactory.create"
}

anyTokens ::= (<< readAny >>)*
fakeTwoDashes ::= FAKETWODASHES
fakeThreeDashes ::= FAKETHREEDASHES

private metaStatement ::= statement { recoverUntil = "meta_statement_recover" }
private meta_statement_recover ::= !(def_statement_recover_start | RBRACE) 
metaCodeBody ::= LBRACE (metaStatement*) RBRACE


//recoverUntil=rule_recover
metaCodeStatement ::= ATSIGN metacodeUsage LBRAC metaCodeIdList RBRAC metaCodeBody? SEMI {pin=2
mixin="com.simpleplugin.psi.references.impl.LSFMetaReferenceImpl" implements="com.simpleplugin.psi.references.LSFMetaReference"}

private rule_recover ::= !(AFTER | ATSIGN | BEFORE | CLASS | CONSTRAINT | DESIGN | END | EXTEND | FORM | GROUP | HIDE | ID | INDEX | LOGGABLE | META | NAVIGATOR | ON | SEMI | SHOWDEP | TABLE | WHEN | WINDOW)

metaDeclId ::=  ID
metaDeclIdList ::=  (metaDeclId (COMMA metaDeclId)*)?

metaCodeIdList ::=  (metaCodeId (COMMA metaCodeId)*)?

metaCodeId ::=  PRIMITIVE_TYPE
	        |	compoundID
	        |	metaCodeLiteral
	        |

metaCodeLiteral ::= LEX_STRING_LITERAL
                | 	LEX_UINT_LITERAL
                |	LEX_UNUMERIC_LITERAL
                |	LEX_UDOUBLE_LITERAL
                |	LEX_ULONG_LITERAL
                |	LEX_LOGICAL_LITERAL
                |	LEX_DATE_LITERAL
                |	LEX_DATETIME_LITERAL
                |	LEX_TIME_LITERAL
                |	LEX_COLOR_LITERAL
                |	NULL

////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// COMMON /////////////////////////////////
////////////////////////////////////////////////////////////////////////////////

emptyStatement ::= SEMI;

mappedPropertyObject ::= propertyUsage LBRAC objectUsageList RBRAC // temporary presentation logic
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}
mappedPropertyClassParamDeclare ::= propertyUsage LBRAC classParamDeclareList RBRAC // temporary domain logic
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList ]}
mappedPropertyExprParam ::= propertyUsage LBRAC exprParameterUsageList RBRAC // temporary mixed logic 
{implements = "com.simpleplugin.psi.context.PropertyUsageContext" methods = [ resolveParamClasses getParamList]}

simpleNameWithCaption ::= simpleName (stringLiteral)?

// local

objectUsageList ::= (nonEmptyObjectUsageList)?
nonEmptyObjectUsageList ::= objectUsage (COMMA objectUsage)*

nonEmptyGroupObjectUsageList ::= groupObjectUsage (COMMA groupObjectUsage)*

// global
customClassUsageList ::= (nonEmptyCustomClassUsageList)?
nonEmptyCustomClassUsageList ::= customClassUsage (COMMA customClassUsage)*

moduleUsageList ::= (nonEmptyModuleUsageList)?
nonEmptyModuleUsageList ::= moduleUsage (COMMA moduleUsage)*

namespaceUsageList ::= (nonEmptyModuleUsageList)?
nonEmptyNamespaceUsageList ::= namespaceUsage (COMMA namespaceUsage)*

classNameList ::= (nonEmptyClassNameList)?
nonEmptyClassNameList ::= className (COMMA className)*


propertyUsageList ::= (nonEmptyPropertyUsageList)?
nonEmptyPropertyUsageList ::= propertyUsage (COMMA propertyUsage)*

nonEmptyNoContextPropertyUsageList ::= noContextPropertyUsage (COMMA noContextPropertyUsage)*

formUsageList ::= (nonEmptyFormUsageList)?
nonEmptyFormUsageList ::= formUsage (COMMA formUsage)*

exprParameterUsageList ::= (exprParameterUsage (COMMA exprParameterUsage)*)?

simpleNameWithCaptionList ::= (nonEmtySimpleNameWithCaptionList)?
nonEmtySimpleNameWithCaptionList ::= simpleNameWithCaption (COMMA simpleNameWithCaption)*

actionPDBList ::= (nonEmptyActionPDBList)?
nonEmptyActionPDBList ::= actionPropertyDefinitionBody (COMMA actionPropertyDefinitionBody)*

propertyExpressionList ::= (nonEmptyPropertyExpressionList)?
nonEmptyPropertyExpressionList ::= propertyExpression (COMMA propertyExpression)*

literal ::=
        ulongLiteral
	|	uintLiteral
	|	udoubleLiteral
	|	unumericLiteral
	|	stringLiteral
	|	booleanLiteral
	|	dateTimeLiteral
	|	dateLiteral
	|	timeLiteral
	|	nullLiteral
	|	staticObjectID
	|	colorLiteral
{implements = "com.simpleplugin.psi.context.FriendlyPE" methods = [ resolveInferredValueClass inferParamClasses ]}

builtInClassName ::= PRIMITIVE_TYPE
{
    mixin="com.simpleplugin.psi.LSFIdImpl"
    implements="com.simpleplugin.psi.LSFId"
}

className ::= builtInClassName | customClassUsage

// local

// parameterUsage is not needed yet, there is either declaration (mappedPropertyParamDeclare), or exprUsage (exprParameterUsage) 

objectUsage ::= simpleName
{mixin="com.simpleplugin.psi.references.impl.LSFObjectReferenceImpl" implements="com.simpleplugin.psi.references.LSFObjectReference"}

groupObjectUsage ::= simpleName
{mixin="com.simpleplugin.psi.references.impl.LSFGroupObjectReferenceImpl" implements="com.simpleplugin.psi.references.LSFGroupObjectReference"}

exprParameterNameUsage ::= classParamDeclare
{mixin="com.simpleplugin.psi.references.impl.LSFExprParamReferenceImpl" implements="com.simpleplugin.psi.references.LSFExprParamReference"}

exprParameterUsage ::= ((DOLLAR)? exprParameterNameUsage) | (DOLLAR LEX_UINT_LITERAL) // can be used for param and object simultaneously, for use in exprs

// global

customClassUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFClassReferenceImpl" implements="com.simpleplugin.psi.references.LSFClassReference"}

moduleUsage ::= simpleName
{mixin="com.simpleplugin.psi.references.impl.LSFModuleReferenceImpl" implements="com.simpleplugin.psi.references.LSFModuleReference"}

namespaceUsage ::= simpleName
{mixin="com.simpleplugin.psi.references.impl.LSFNamespaceReferenceImpl" implements="com.simpleplugin.psi.references.LSFNamespaceReference"}

explicitPropClass ::= className | QUESTION 
nonEmptyExplicitPropClassList ::= explicitPropClass (COMMA explicitPropClass)*
propertyUsage ::= compoundID (LESS nonEmptyExplicitPropClassList GREATER)?
{mixin="com.simpleplugin.psi.references.impl.LSFPropReferenceImpl" implements="com.simpleplugin.psi.references.LSFPropReference"}

tableUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFTableReferenceImpl" implements="com.simpleplugin.psi.references.LSFTableReference"}

groupUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFGroupReferenceImpl" implements="com.simpleplugin.psi.references.LSFGroupReference"}

formUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFFormReferenceImpl" implements="com.simpleplugin.psi.references.LSFFormReference"}

windowUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFWindowReferenceImpl" implements="com.simpleplugin.psi.references.LSFWindowReference"}

navigatorElementUsage ::= compoundID
{mixin="com.simpleplugin.psi.references.impl.LSFNavigatorElementReferenceImpl" implements="com.simpleplugin.psi.references.LSFNavigatorElementReference"}

metacodeUsage ::= compoundID

typeId ::= PRIMITIVE_TYPE | OBJECT

compoundID ::= (<<innerIDStop>> namespaceUsage POINT simpleName) | simpleName

staticObjectID ::= <<fullCompoundParamDeclareStop>> <<innerIDCheck>> customClassUsage POINT simpleName
{mixin="com.simpleplugin.psi.references.impl.LSFStaticObjectReferenceImpl" implements="com.simpleplugin.psi.references.LSFStaticObjectReference"}

groupObjectID ::= <<innerIDCheck>> formUsage POINT groupObjectUsage
{implements="com.simpleplugin.psi.context.FormContext" }

objectID ::= <<innerIDCheck>> formUsage POINT objectUsage
{implements="com.simpleplugin.psi.context.FormContext" }

multiCompoundID ::= ID (POINT ID)*

exclusiveOverrideOption ::= OVERRIDE | EXCLUSIVE

colorLiteral ::= LEX_COLOR_LITERAL | (RGB LBRAC uintLiteral COMMA uintLiteral COMMA uintLiteral RBRAC)
stringLiteral ::= LEX_STRING_LITERAL
intLiteral ::= (MINUS)? uintLiteral
longLiteral ::=  (MINUS)? ulongLiteral
doubleLiteral ::= (MINUS)? unumericLiteral
dateLiteral ::= LEX_DATE_LITERAL
dateTimeLiteral ::= LEX_DATETIME_LITERAL
timeLiteral ::= LEX_TIME_LITERAL
booleanLiteral ::= LEX_LOGICAL_LITERAL
udoubleLiteral ::= LEX_UDOUBLE_LITERAL
unumericLiteral ::= LEX_UNUMERIC_LITERAL
uintLiteral ::= LEX_UINT_LITERAL
ulongLiteral ::= LEX_ULONG_LITERAL
nullLiteral ::= NULL

nullOption ::= NULL

dimensionLiteral ::= LBRAC intLiteral COMMA intLiteral RBRAC
boundsIntLiteral ::= LBRAC intLiteral COMMA intLiteral COMMA intLiteral COMMA intLiteral RBRAC
boundsDoubleLiteral ::= LBRAC doubleLiteral COMMA doubleLiteral COMMA doubleLiteral COMMA doubleLiteral RBRAC

simplexConstraintLiteral ::=
        TO THE LEFT
	|	TO THE RIGHT
	|	TO THE BOTTOM
	|	TO THE RIGHTBOTTOM
	|	TO NOT INTERSECT

insertRelativePositionLiteral ::= BEFORE | AFTER

containerTypeLiteral ::=    CONTAINERV
	                    |	CONTAINERH
	                    |	COLUMNS
	                    |	TABBED
	                    |	SPLITH
	                    |	SPLITV
	                    
alignmentLiteral ::= LEADING | CENTER | TRAILING

flexAlignmentLiteral ::= LEADING | CENTER | TRAILING | STRETCH


propertyEditTypeLiteral ::= EDITABLE | READONLY | SELECTOR

modalityTypeLiteral ::= DOCKED | MODAL | DOCKEDMODAL | FULLSCREEN

formPrintTypeLiteral ::= PRINT (AUTO)?

formSessionScopeLiteral ::= OLDSESSION | NEWSESSION | MANAGESESSION

emailRecipientTypeLiteral ::= TO | CC | BCC

emailAttachFormat ::= PDF | DOCX | HTML | RTF

